/*
 * Name:	Campaign
 * Date:	2020-07-27
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * The base class for a campaign. Inherit this to create your own campaign.
 */

using Mission.Common;

namespace Campaigns
{
	public class Campaign
	{
		// Creates a Campaign object.
		public constructor()
		{
			private ["_missions" as Array /* of Mission */];
			
			_missions = [];
			_self.GainedTokensWest = [];
			_self.LostTokensWest = [];
			_self.GainedTokensEast = [];
			_self.LostTokensEast = [];
			
			[_missions] call _self.SelectMissions;
			_self.Missions = _missions;
			
			_self.CurrentMissionIndex = -1;
		};
		
		protected virtual method SelectMissions("_missions" as Array /* of Mission */)
		{
		};
		
		protected property Scalar CurrentMissionIndex { get; protected set; };
		
		protected property Array Missions { get; protected set; };
		
		public property Array GainedTokensWest { get; private set; };
		
		public property Array GainedTokensEast { get; private set; };
		
		public property Array LostTokensWest { get; private set; };
		
		public property Array LostTokensEast { get; private set; };
		
		// Gets the next mission in the campaign. Used when a mission has ended and a new is about to start.
		// Default behavior is to simply return the next mission in the mission array.
		// _lastMissionEndNarrative (String): The end narrative of the last mission played (that was set by the mission itself).
		// Returns (Mission): The next mission to play. classNull if the campaign is over.
		public virtual method Mission GetNextMission("_lastMissionEndNarrative" as String)
		{
			_self.CurrentMissionIndex = _self.CurrentMissionIndex + 1;
			
			if (_self.CurrentMissionIndex < count _self.Missions) then {
				return _self.Missions select _self.CurrentMissionIndex;
			};
			
			return classNull;
		};
	};
};
