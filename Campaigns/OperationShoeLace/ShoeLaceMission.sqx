/*
 * Name:	ShoeLaceMission
 * Date:	2020-08-03
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Contains common logic for all mission that belong to Operation Shoe Lace.
 */

using Tvtcf.Common;
using Tvtcf.Server;
using Campaigns;

namespace Campaigns.ShoeLace
{
	public class ShoeLaceMission : Mission
	{
		// Creates a ShoeLaceMission object.
		public constructor()
		{
			call _base.Constructor;
		};
		
		protected override method InitMission("_mission" as MissionSettings)
		{
			_mission.Name = "A Shoe Lace Mission";
		};
		
		protected property Object ActingsQuadbike { get; private set; };
		
		protected property Object OpposingsQuadbike { get; private set; };
		
		protected property Object ActingsMrap { get; private set; };
		
		protected property Object OpposingsMrap { get; private set; };
		
		protected override method String AddBriefingInfoForActing()
		{
			return [MissionSide.Acting] call _self.GetTokenBriefingText;
		};
		
		protected override method String AddBriefingInfoForOpposing()
		{
			return [MissionSide.Opposing] call _self.GetTokenBriefingText;
		};
		
		protected method GetTokenBriefingText("_missionSide" as MissionSide)
		{
			var _info = "<br /><br />Gained tokens: ";
			var _comma = "";
			
			if (_missionSide == MissionSide.Acting) then {
				{
					_info = _info + _comma + _x;
					_comma = ", ";
				} foreach _self.GainedTokensActing as String;
			}
			else {
				{
					_info = _info + _comma + _x;
					_comma = ", ";
				} foreach _self.GainedTokensOpposing as String;
			};
			
			_info = _info + ".<br />Lost tokens: ";
			
			_comma = "";
			
			if (_missionSide == MissionSide.Acting) then {
				{
					_info = _info + _comma + _x;
					_comma = ", ";
				} foreach _self.LostTokensActing as String;
			}
			else {
				{
					_info = _info + _comma + _x;
					_comma = ", ";
				} foreach _self.LostTokensOpposing as String;
			};
			
			return _info;
		};
		
		protected override method Setup()
		{
			[MissionSide.Acting, false] call _self.SetIndependentAlliance;
			[MissionSide.Opposing, false] call _self.SetIndependentAlliance;
			
			if ("BASIC WEAPONS" in _self.GainedTokensActing) then {
				["AMMOBOX BASIC WEAPONS", BaseMarker.AmmoCrate, MissionSide.Acting, true] call _self.CreateSideVehicleInBase;
				["AMMOBOX BASIC AMMO", BaseMarker.AmmoCrate, MissionSide.Acting, true] call _self.CreateSideVehicleInBase;
			};
			
			if ("BASIC WEAPONS" in _self.GainedTokensOpposing) then {
				["AMMOBOX BASIC WEAPONS", BaseMarker.AmmoCrate, MissionSide.Opposing, true] call _self.CreateSideVehicleInBase;
				["AMMOBOX BASIC AMMO", BaseMarker.AmmoCrate, MissionSide.Opposing, true] call _self.CreateSideVehicleInBase;
			};
			
			if ("AAF EXPLOSIVES" in _self.GainedTokensActing) then {
				var _box = ["AMMOBOX ORDNANCE", BaseMarker.AmmoCrate, MissionSide.Acting, true] call _self.CreateSideVehicleInBase;
				_box addBackpackCargoGlobal ["B_Kitbag_cbr", 1];
			};
		
			if ("AAF EXPLOSIVES" in _self.GainedTokensOpposing) then {
				var _box = ["AMMOBOX ORDNANCE", BaseMarker.AmmoCrate, MissionSide.Opposing, true] call _self.CreateSideVehicleInBase;
				_box addBackpackCargoGlobal ["B_Kitbag_cbr", 1];
			};
		
			_self.ActingsQuadbike = ["QUADBIKE", BaseMarker.LandVehicles, MissionSide.Acting, true] call _self.CreateSideVehicleInBase;
			_self.ActingsMrap = ["MRAP", BaseMarker.LandVehicles, MissionSide.Acting, true] call _self.CreateSideVehicleInBase;
			_self.OpposingsQuadbike = ["QUADBIKE", BaseMarker.LandVehicles, MissionSide.Opposing, true] call _self.CreateSideVehicleInBase;
			_self.OpposingsMrap = ["MRAP", BaseMarker.LandVehicles, MissionSide.Opposing, true] call _self.CreateSideVehicleInBase;
			
			call _base.Setup;
		};
		
		protected method SetIndependentAlliance("_missionSide" as MissionSide, "_setAsAllied" as Boolean)
		{
			var _side = [_missionSide] call _self.GetActualSide;
			var _value = 1;
			
			if (!_setAsAllied) then {
				_value = 0;
			};
			
			resistance setFriend [_side, _value];
			_side setFriend [resistance, _value];
		};
	};
};
