/*
 * Name:	OperationShoeLace
 * Date:	2020-07-27
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Serious campaign with a story.
 */

using Mission.Common;
using Mission.Server;
using Campaigns;

namespace Campaigns.ShoeLace
{
	public class OperationShoeLace : Campaign
	{
		private fields ["_mCaptureTheAirbase" as Mission, "_mEstablishments" as Mission, "_mHawkDown" as Mission, "_mNightTensions" as Mission, "_mCatAndMouse" as Mission, "_mAirStationAssault" as Mission, "_mCannonballRun" as Mission];
		
		// Creates a OperationShoeLace object.
		public constructor()
		{
			call _base.Constructor;
		};
		
		protected override method SelectMissions("_missions" as Array /* of Mission */)
		{
			_mEstablishments = new Establishments;
			[selectRandom [west, east]] call _mEstablishments.SetActingSide;
			_missions pushBack _mEstablishments;
			
			_mCaptureTheAirbase = new CaptureTheAirbase;
			[selectRandom [west, east]] call _mCaptureTheAirbase.SetActingSide;
			_missions pushBack _mCaptureTheAirbase;
			
			_mHawkDown = new HawkDown;
			_missions pushBack _mHawkDown;
			
			_mNightTensions = new NightTensions;
			_missions pushBack _mNightTensions;
			
			_mCatAndMouse = new CatAndMouse;
			[selectRandom [west, east]] call _mCatAndMouse.SetActingSide;
			_missions pushBack _mCatAndMouse;
			
			_mAirStationAssault = new AirStationAssault;
			_missions pushBack _mAirStationAssault;
			
			_mCannonballRun = new CannonballRun;
			[selectRandom [west, east]] call _mCannonballRun.SetActingSide;
			_missions pushBack _mCannonballRun;
		};
		
		public override method Mission GetNextMission("_lastMissionEndNarrative" as String)
		{
			private ["_mission" as Mission];
		
			_mission = [_lastMissionEndNarrative] call _base.GetNextMission;
			
			if (!isNull _mission) then
			{
				// In the Hawk Down mission, the side that owns the airbase is about to blow up the helicopter
				if (_mission == _mHawkDown) then
				{
					if ("AIRBASE" in _self.GainedTokensWest) then {
						[west] call _mission.SetActingSide;
					}
					else {
						[east] call _mission.SetActingSide;
					};
				};
				
				// In the Night Tensions mission, the acting side is the side not owning the airbase
				if (_mission == _mNightTensions) then
				{
					if ("AIRBASE" in _self.GainedTokensWest) then {
						[east] call _mission.SetActingSide;
					}
					else {
						[west] call _mission.SetActingSide;
					};
				};
				
				// In the Air Station Assault mission, the defending side will be the ones not defending the Airbase in Night Tensions
				if (_mission == _mAirStationAssault) then
				{
					if ("AIRBASE" in _self.GainedTokensWest) then {
						[west] call _mission.SetActingSide;
					}
					else {
						[east] call _mission.SetActingSide;
					};
				};
			};
			
			return _mission;
		};
	};
};
