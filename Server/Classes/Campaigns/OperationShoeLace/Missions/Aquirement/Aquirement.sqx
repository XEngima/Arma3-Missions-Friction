/*
 * Name:	Aquirement
 * Date:	2020-08-03
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * First mission of Operation Shoe Lace, in which the sides select their vehicles and equipment.
 */

using Mission.Common;
using Mission.Server;

namespace Mission.Server.OperationShoeLace
{
	public class Aquirement : ShoeLaceMission
	{
		private fields ["_mQuadbikeActing" as Object, "_mMrapActing" as Object, "_mQuadbikeOpposing" as Object, "_mMrapOpposing" as Object];
		private fields ["_mQuadbikeActingActionId" as Scalar];
	
		// Creates a Aquirement object.
		public constructor("_actingSide" as Side)
		{
			[_actingSide] call _base.Constructor;
		};
		
		protected override method InitMission("_mission" as MissionSettings)
		{
			_mission.Name = "Aquirement";
			_mission.StartTimeOfDay = 16;
		};
		
		protected override method Setup()
		{
			_mQuadbikeActing = ["QUADBIKE", MissionSide.Acting, BaseMarker.LandVehicles, true] call _self.CreateSideVehicleOnBaseMarker;
			_mMrapActing = ["MRAP", MissionSide.Acting, BaseMarker.LandVehicles, true] call _self.CreateSideVehicleOnBaseMarker;
			_mQuadbikeOpposing = ["QUADBIKE", MissionSide.Opposing, BaseMarker.LandVehicles, true] call _self.CreateSideVehicleOnBaseMarker;
			_mMrapOpposing = ["MRAP", MissionSide.Opposing, BaseMarker.LandVehicles, true] call _self.CreateSideVehicleOnBaseMarker;
			
			_mQuadbikeActingActionId = [_mQuadbikeActing, "Select this as transport", MissionSide.Acting] call _self.AddAction;
			[_mMrapActing, "Select this as transport", MissionSide.Acting] call _self.AddAction;
			[_mQuadbikeOpposing, "Select this as transport", MissionSide.Acting] call _self.AddAction;
			[_mMrapOpposing, "Select this as transport", MissionSide.Acting] call _self.AddAction;
			
			call _base.Setup;
		};
		
		public override method OnAction("_actionId" as Scalar, "_target" as Object, "_actor" as Object)
		{
			if (_target == _mQuadbikeActing) then {
				_self.GainedTokenActing = HistoryToken.Quadbike;
				[MissionSide.Acting, "Quadbike Selected"] call _self.ShowNotification;
				[[_mQuadbikeActingActionId]] call _self.DeleteActions;
			};
			if (_target == _mMrapActing) then {
				_self.GainedTokenActing = HistoryToken.Mrap;
				[MissionSide.Acting, "MRAP Selected"] call _self.ShowNotification;
			};
			if (_target == _mQuadbikeOpposing) then {
				_self.GainedTokenOpposing = HistoryToken.Quadbike;
				[MissionSide.Opposing, "Quadbike Selected"] call _self.ShowNotification;
			};
			if (_target == _mMrapOpposing) then {
				_self.GainedTokenOpposing = HistoryToken.Mrap;
				[MissionSide.Opposing, "MRAP Selected"] call _self.ShowNotification;
			};
		};
		
		protected override method String AddBriefingInfoForActing()
		{
			return "Enter the vehicle that you want to use as your transport vehicle during the campaign.";
		};
		
		protected override method String AddBriefingInfoForOpposing()
		{
			return "Enter the vehicle that you want to use as your transport vehicle during the campaign.";
		};
		
		protected override method MissionObjective CreatePrimaryObjectiveForActing()
		{
			return ["Select a vehicle", "Enter the vehicle that you want to use as your transport vehicle during the campaign."] new MissionObjective;
		};
		
		protected override method MissionObjective CreatePrimaryObjectiveForOpposing()
		{
			return ["Select a vehicle", "Enter the vehicle that you want to use as your transport vehicle during the campaign."] new MissionObjective;
		};
		
		protected override method ObjectiveState CheckPrimaryObjectiveStateActing()
		{
			if (!isNull _self.GainedTokenActing) then {
				return ObjectiveState.Succeeded;
			};
			
			return ObjectiveState.None;
		};
		
		protected override method ObjectiveState CheckPrimaryObjectiveStateOpposing()
		{
			if (!isNull _self.GainedTokenOpposing) then {
				return ObjectiveState.Succeeded;
			};
			
			return ObjectiveState.None;
		};
	};
};
