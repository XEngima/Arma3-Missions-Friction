/*
 * Name:	CameraHandler
 * Date:	2020-07-08
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Handles all about camera views - for dead players.
 */

using Tvtcf.Communication;
using Tvtcf.Client.Statics;

namespace Tvtcf.Client
{
	public class CameraHandler
	{
		private fields ["_mCameraUnit" as Object, "_mClientMessageHandler" as ClientMessageHandler];
	
		// Creates a CameraHandler object.
		public constructor("_clientMessageHandler" as ClientMessageHandler)
		{
			_mClientMessageHandler = _clientMessageHandler;
			_mCameraUnit = objNull;
		};
		
		public method SetDeathCamera()
		{
			private _hasInserted = false;
			private _missionUnits = (call PublicVariableHandler.GetInsertedWestlyUnits) + (call PublicVariableHandler.GetInsertedEastlyUnits);
		
			if (playerSide == west) then {
				_hasInserted = call PublicVariableHandler.GetWestHasInserted;
			}
			else {
				_hasInserted = call PublicVariableHandler.GetEastHasInserted;
			};
			
			if (_hasInserted && !ClientState.IsAlive) then
			{
				// If the unit we are currently following has logged out or is dead, then switch camera
				if (isNull _mCameraUnit || { !(alive _mCameraUnit) }) then
				{
					// First, switch to a player in the same group
					{
						if (alive _x && isPlayer _x && (side group _x == playerSide)) then {
							switchCamera _x;
							_mCameraUnit = _x;
							["Following " + name _x] call _mClientMessageHandler.ShowMessage;
							return;
						};
					} foreach _missionUnits;
					
					// Second, switch to a player on the other side
					{
						if (alive _x && isPlayer _x) then {
							switchCamera _x;
							_mCameraUnit = _x;
							["Following " + name _x] call _mClientMessageHandler.ShowMessage;
							return;
						};
					} foreach _missionUnits;
					
					// Third, switch to AI in the same group
					{
						if (alive _x && (side group _x == playerSide)) then {
							switchCamera _x;
							_mCameraUnit = _x;
							["Following " + name _x] call _mClientMessageHandler.ShowMessage;
							return;
						};
					} foreach _missionUnits;
					
					// Third, switch to AI in the other group
					{
						if (alive _x) then {
							switchCamera _x;
							_mCameraUnit = _x;
							["Following " + name _x] call _mClientMessageHandler.ShowMessage;
							return;
						};
					} foreach _missionUnits;
				};
			};
		};
		
		public method ResetPlayerCamera()
		{
			switchCamera player;
			_mCameraUnit = objNull;
		};
	};
};
