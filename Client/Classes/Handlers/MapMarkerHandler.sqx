/*
 * Name:	MapMarkerHandler
 * Date:	2020-06-26
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Handles map markers on the client.
 */

using Tvtcf.Common;

namespace Tvtcf.Client
{
	public class MapMarkerHandler
	{
		private fields ["_mLocalMarkers" as Array, "_mInsertionMarker" as String];
		
		// Creates a MapMarkerHandler object.
		public constructor()
		{
			_mLocalMarkers = [];
			_self.UseExtractionMarker = false;
			_mInsertionMarker = "";
		};
		
		public property Boolean UseExtractionMarker { get; private set; };
		
		public method AddExtractionMarker("_position" as Array)
		{
			if (_self.UseExtractionMarker) then
			{
				private _marker = createMarkerLocal ["_extractionMarkerLocal", _position];
				_marker setMarkerShapeLocal "ICON";
				_marker setMarkerTypeLocal "mil_end";
				
				_mLocalMarkers pushBack _marker;
			};
		};
		
		public method UpdateMarkers("_missionMarkersInfo" as MissionMarkersInfo)
		{
			_self.UseExtractionMarker = [playerSide] call _missionMarkersInfo.CheckUseExtractionMarker;
		
			// Remove all earlier markers
			
			{
				deleteMarkerLocal _x;
			} foreach _mLocalMarkers;
			
			_mLocalMarkers = [];
			
			if (_mInsertionMarker != "") then {
				deleteMarkerLocal _mInsertionMarker;
				_mInsertionMarker = "";
			};
		
			// Draw start markers
			
			private _startMarkerColor = if (playerSide == west) then { "colorBLUFOR" } else { "colorOPFOR" };
			var _startMarkers = [];
			
			{
				if (_x.Side == playerSide) then
				{
					private _name = _x.Name + "_local";
					private _pos = getMarkerPos _x.Name;
					
					private _marker = createMarkerLocal [_name, _pos];
					_mLocalMarkers pushBack _marker;
					_startMarkers pushBack _marker;

					_marker setMarkerShapeLocal (markerShape _x.Name);
					_marker setMarkerSizeLocal (markerSize _x.Name);
					_marker setMarkerDirLocal (markerDir _x.Name);
					_marker setMarkerColorLocal _startMarkerColor;
					_marker setMarkerBrushLocal "DiagGrid";
				};
			} foreach _missionMarkersInfo.StartMarkers as MarkerInfo;
			
			// Insertion marker
			
			if (count _startMarkers > 0) then {
				var _pos = [_startMarkers] call Functions.FindClosestToMarkersPos;
				
				_mInsertionMarker = createMarkerLocal ["_insertionMarkerLocal", _pos];
				_mInsertionMarker setMarkerShapeLocal "ICON";
				_mInsertionMarker setMarkerTypeLocal "mil_start";
			};
			
			// Draw other markers
			
			{
				if (_x.Side == playerSide) then {
					private _name = _x.Name + "_local";
					private _pos = getMarkerPos _x.Name;
					
					private _marker = createMarkerLocal [_name, _pos];
					_mLocalMarkers pushBack _marker;
					
					_marker setMarkerShapeLocal (markerShape _x.Name);
					_marker setMarkerSizeLocal (markerSize _x.Name);
					_marker setMarkerDirLocal (markerDir _x.Name);
					_marker setMarkerColorLocal (markerColor _x.Name);
					_marker setMarkerBrushLocal (markerBrush _x.Name);
					_marker setMarkerTypeLocal (markerType _x.Name);
					_marker setMarkerTextLocal (markerText _x.Name);
				};
			} foreach _missionMarkersInfo.Markers as MarkerInfo;
		};
	};
};
