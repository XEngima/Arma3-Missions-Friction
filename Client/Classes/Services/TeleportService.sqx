/*
 * Name:	TeleportService
 * Date:	2020-07-03
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Performs teleportations of the player.
 */

using Sqx.Services;

namespace Mission.Client
{
	public class TeleportService : Service
	{
		private fields ["_mPosition" as Array];
		
		// Creates a TeleportService object.
		public constructor("_position" as Array)
		{
			call _base.Constructor;
			_mPosition = _position;
		};
		
		// Teleports the player instantly to the base.
		protected override method Run()
		{
			cutText ["", "WHITE OUT", 1];
			
			sleep 1.2;
			
			private _vehiclesTeleported = [];
			
			{
				private _vehicle = vehicle _x;
				
				if (!(_vehicle in _vehiclesTeleported)) then {
					_vehicle setPos [(_mPosition select 0) - 3 + random 6, (_mPosition select 1) - 3 + random 6, 0.1];
					_vehiclesTeleported pushBack _vehicle;
				};
			} foreach units group player;
			
			openMap false;
			
			cutText ["", "WHITE IN", 1];
			
			call _base.Run;
		};
	};
};
