/*
 * Name:	TeleportService
 * Date:	2020-07-03
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Performs teleportations of the player.
 */

using Sqx.Services;

namespace Mission.Client
{
	public class TeleportService : Service
	{
		private fields ["_mPosition" as Array];
		
		// Creates a TeleportService object.
		public constructor("_position" as Array)
		{
			call _base.Constructor;
			_mPosition = _position;
		};
		
		private method TeleportVehicleAndCrew("_vehicle" as Object)
		{
			_vehicle setPos [(_mPosition select 0) - 3 + random 6, (_mPosition select 1) - 3 + random 6, 0.1];
		};
		
		// Teleports the player instantly to the base.
		protected override method Run()
		{
			if (player distance _mPosition > 50) then
			{
				cutText ["", "BLACK OUT", 0.5];
				
				sleep 0.5;
				
				private _teleportedUnits = [];
				
				{
					private _unit = vehicle _x;
					private _isVehicle = vehicle _x != _x;
	
					if (_isVehicle) then
					{
						if (local _unit && { !(_unit in _teleportedUnits) }) then
						{
							[_unit] call _self.TeleportVehicleAndCrew;
							_teleportedUnits pushBack _unit;
						};
					}
					else // Is single unit
					{
						if (local _unit && { !(_unit in _teleportedUnits) }) then {
							_unit setPos [(_mPosition select 0) - 3 + random 6, (_mPosition select 1) - 3 + random 6, 0.1];
							_teleportedUnits pushBack _unit;
						};
					};
				} foreach units group player;
				
				openMap false;
				cutText ["", "BLACK IN", 0.5];
			};
			
			call _base.Run;
		};
	};
};
